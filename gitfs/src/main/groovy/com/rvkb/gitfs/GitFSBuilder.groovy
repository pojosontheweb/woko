package com.rvkb.gitfs

import org.eclipse.jgit.api.Git
import org.eclipse.jgit.storage.file.FileRepositoryBuilder
import org.eclipse.jgit.storage.file.FileRepository

class GitFSBuilder {

  private File pathToRepo
  private boolean createIfEmpty = true

  private void debug(def msg) {
    println "[GitFSBuilder] $msg"
  }

  GitFSBuilder(String pathToRepo) {
    assert pathToRepo != null
    debug "Creating with path $pathToRepo"
    this.pathToRepo = new File(pathToRepo)
  }

  GitFSBuilder(File pathToRepo) {
    assert pathToRepo != null
    debug "Creating with file $pathToRepo.absolutePath"
    this.pathToRepo = pathToRepo
  }

  GitFSBuilder setCreateIfEmpty(boolean createIfEmpty) {
    this.createIfEmpty = createIfEmpty
    return this
  }

  boolean getCreateIfEmpty() {
    return createIfEmpty
  }

  File getPathToRepo() {
    return pathToRepo
  }

  GitFS build() {
    int i = pathToRepo.absolutePath.indexOf(".git")
    if (!pathToRepo.exists()) {
      if (!createIfEmpty) {
        throw new IllegalStateException("path $pathWithoutGit ain't a valid file, and createIfEmpty is false. Cannot initialize.")
      }
      debug "$pathToRepo doesn't exist, initializing repo"
      String pathWithoutGit = pathToRepo.absolutePath
      if (i!=-1) {
        pathWithoutGit = pathWithoutGit.substring(0, i)
      }
      File f = new File(pathWithoutGit)
      // create directory and init git repo
      if (!f.exists()) {
        f.mkdirs()
        debug("Created directory $f.absolutePath")
      }
      debug("Initializing repo in $pathToRepo.absolutePath...")
      FileRepository fr = new FileRepositoryBuilder().setGitDir(pathToRepo).build()
      Git git = new Git(fr)
      git.init().setDirectory(f).call()
      debug("...$pathToRepo.absolutePath initialized")

      new File(pathWithoutGit + "/gitfs.txt").withWriter { w->
        w << "File generated by GitFS."
      }
      git.add().addFilepattern("gitfs.txt").call()
      git.commit().setAuthor("GifFS", "nomail").setMessage("Initial commit").call()
      fr.close()
      debug("Repository created in $pathToRepo.absolutePath")
    } else {
      debug("Using existing repository in $pathToRepo.absolutePath")
    }
    return new GitFS(pathToRepo)
  }

}
